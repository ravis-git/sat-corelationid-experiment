buildscript {
	ext {
		springBootVersion = '1.5.6.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('se.transmode.gradle:gradle-docker:1.2')
	}
}

apply plugin: 'java'
apply plugin: 'docker'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'predix-aggregate-service'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
    compile(
            'org.springframework.boot:spring-boot-starter',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.springframework.boot:spring-boot-starter-jersey',
            'org.projectlombok:lombok:1.16.18',
            'org.aspectj:aspectjweaver:1.8.10'
    )

    //ops
    compile('org.jolokia:jolokia-core')

    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'org.hamcrest:hamcrest-core:1.3'
    )
}

task buildDocker(type: Docker, dependsOn: [build]) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    tag = 'rpalakodeti/'+jar.baseName
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

test {
    testLogging {
        // set options for log level LIFECYCLE
        events "failed"
        exceptionFormat "short"
        //showStandardStreams = true

        // set options for log level DEBUG
        lifecycle {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["passed","failed", "skipped"]
    }
}
